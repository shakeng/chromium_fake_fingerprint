# Copyright 2020 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/android/rules.gni")

android_library("java") {
  sources = [
    "java/src/org/chromium/chrome/browser/nfc/NfcSystemLevelPrompt.java",
    "java/src/org/chromium/chrome/browser/nfc/NfcSystemLevelSetting.java",
  ]

  annotation_processor_deps = [ "//base/android/jni_generator:jni_processor" ]

  deps = [
    ":java_resources",
    "//base:base_java",
    "//base:jni_java",
    "//content/public/android:content_java",
    "//third_party/android_deps:android_support_v7_appcompat_java",
    "//ui/android:ui_full_java",

    # TODO(crbug.com/1017190): Remove the following 2 deps once we stop linting individual targets.
    "//components/browser_ui/modaldialog/android:java",
    "//third_party/android_deps:com_android_support_design_java",
  ]
}

generate_jni("jni_headers") {
  sources =
      [ "java/src/org/chromium/chrome/browser/nfc/NfcSystemLevelSetting.java" ]
}

android_resources("java_resources") {
  deps = [
    "//chrome/browser/ui/android/strings:ui_strings_grd",
    "//components/browser_ui/modaldialog/android:java_resources",
    "//components/browser_ui/strings/android:browser_ui_strings_grd",
  ]
  resource_dirs = [ "java/res" ]
  custom_package = "org.chromium.chrome.browser.nfc"
}

source_set("native") {
  sources = [
    "nfc_system_level_setting.h",
    "nfc_system_level_setting_impl.cc",
    "nfc_system_level_setting_impl.h",
  ]
  deps = [
    ":jni_headers",
    "//base",
    "//content/public/browser",
  ]
}

source_set("test_support") {
  testonly = true
  sources = [
    "mock_nfc_system_level_setting.cc",
    "mock_nfc_system_level_setting.h",
  ]
  deps = [
    ":native",
    "//base",
  ]
}
